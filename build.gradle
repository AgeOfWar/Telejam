group 'me.palazzomichi.telegram'
version '7.1'
ext.versionName = "Peach"

apply plugin: 'java'
apply plugin: 'org.junit.platform.gradle.plugin'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8

dependencies {
    compile group: 'com.google.code.gson',
            name: 'gson',
            version: '2.8.1'

    testCompile group: 'org.junit.jupiter',
            name:'junit-jupiter-api',
            version: '5.0.0-M4'

    testRuntime group:'org.junit.jupiter',
            name:'junit-jupiter-engine',
            version: '5.0.0-M4'
}

jar {
    appendix = versionName
}

// lib with dependencies jar
task libWithDependenciesJar(type: Jar) {
    appendix = versionName
    classifier = 'with-dependencies'
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    with jar
}

// javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    appendix = versionName
    classifier = 'javadoc'
    from javadoc
}

// add source jar
task sourcesJar(type: Jar) {
    appendix = versionName
    classifier = 'sources'
    from sourceSets.main.allSource
}

build.dependsOn libWithDependenciesJar, javadocJar, sourcesJar
