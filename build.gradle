plugins {
    id 'java'
    id 'maven-publish'
}

ext {
    versionName = "Peach"
}

group 'io.github.ageofwar'
version '7.16'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8

dependencies {
    compile 'com.google.code.gson:gson:2.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

test {
    useJUnitPlatform()
}

jar {
    appendix = versionName
}

// lib with dependencies jar
task libWithDependenciesJar(type: Jar) {
    appendix = versionName
    classifier = 'with-dependencies'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

// javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    appendix = versionName
    classifier = 'javadoc'
    from javadoc
}

// add source jar
task sourcesJar(type: Jar) {
    appendix = versionName
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives libWithDependenciesJar
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenDefault(MavenPublication) {
            from components.java
            artifact(sourcesJar) {
                classifier "sources"
            }
        }
    }
}
